(*
 * SPDX-FileCopyrightText: Copyright (C) 2016-present, Arm Limited and/or its affiliates <open-source-office@arm.com>
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * The Armv8 Application Level Memory Model.
 *
 * This is a machine-readable, executable and formal artefact, which aims to be
 * the latest stable version of the Armv8 memory model.
 * If you have comments on the content of this file, please send an email to
 * memory-model@arm.com
 * For a textual version of the model, see section B2.3 of the Armv8 ARM:
 * https://developer.arm.com/documentation/ddi0487/
 *
 * Authors:
 * Jade Alglave <jade.alglave@arm.com>
 * Artem Khyzha <artem.khyzha@arm.com>
 *)
catdep (* This option tells that the cat file computes dependencies *)

include "aarch64util.cat"

(* Local write or MMU Fault successor *)
let lwfs =
 [Exp & M | Imp & Tag & R]; (po & same-loc); [Exp & W]
 | [Exp & M]; (po & same-loc); [MMU & FAULT]
 | [Imp & TTD & R]; (po & same-loc); [Exp & W | HU]

(* Local read successor *)
let lrs = [W]; ((po & same-loc) & ~(intervening(W,(po & same-loc)))); [R]

(* Dependency through registers and memory *)
let rec dtrm =
  [~range(lxsx)]; rf-reg
  | lrs 
  | iico_data
  | dtrm; dtrm

(** Data, Address and Control dependencies *)

let basic-dep =
   [Exp & R | Rreg]; dtrm?
let data = [Exp & R]; (basic-dep; [DATA]; iico_data+; [Exp & W]) & ~same-instance
let addr = [Exp & R]; (basic-dep; [ADDR]; iico_data+; [Exp & M | Imp & Tag & R | Imp & TTD & R | HU | TLBI | DC.CVAU | IC.IVAU]) & ~same-instance
let ctrl = [Exp & R]; basic-dep; [BCC]; po

(** Pick dependencies *)
let rec pick-dtrm =
  dtrm
  | iico_ctrl
  | pick-dtrm; pick-dtrm

let pick-basic-dep =
   [Exp & R | Rreg];  pick-dtrm?

let pick-addr-dep =
   [Exp & R]; (pick-basic-dep; [ADDR]; iico_data+; [Exp & M | Imp & Tag & R | Imp & TTD & R | HU | TLBI | DC.CVAU | IC.IVAU]) & ~same-instance
let pick-data-dep =
   [Exp & R]; (pick-basic-dep; [DATA]; (iico_data|iico_ctrl)+; [Exp & W]) & ~same-instance
let pick-ctrl-dep =
   [Exp & R]; pick-basic-dep; [BCC]; po

let pick-dep =
   ( pick-basic-dep
   | pick-addr-dep
   | pick-data-dep
   | pick-ctrl-dep
   ) & ~same-instance

include "aarch64show.cat"

