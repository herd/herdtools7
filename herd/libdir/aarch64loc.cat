(*
 * SPDX-FileCopyrightText: Copyright (C) 2016-present, Arm Limited and/or its affiliates <open-source-office@arm.com>
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * The Armv8 Application Level Memory Model.
 *
 * This is a machine-readable, executable and formal artefact, which aims to be
 * the latest stable version of the Armv8 memory model.
 * If you have comments on the content of this file, please send an email to
 * memory-model@arm.com
 * For a textual version of the model, see section B2.3 of the Armv8 ARM:
 * https://developer.arm.com/documentation/ddi0487/
 *)

(* Translation-intrinsically-before *)
let tr-ib =
  [Imp & TTD & R]; iico_data; [B]; iico_ctrl; [Exp & M | MMU & FAULT]

(* Notions of Same Location - PA, VA, and including Fault Effects *)
let TTD-same-oa =  same-oa(TTD*TTD) 
let same-loc = [M]; loc; [M]
  | [MMU & Translation & FAULT]; same-low-order-bits
  | same-low-order-bits; [MMU & Translation & FAULT] 
  | (tr-ib^-1; TTD-same-oa; tr-ib) & same-low-order-bits
let po-loc = po & same-loc

(* Same-cache-line relation *)
(* NOTE : currently assumes all locations are on different cache lines *)
(*        also extends to IC without a location specified              *)
let scl = loc
  | (M | DC.CVAU | IC) * (IC.IALLU | IC.IALLUIS)
  | (IC.IALLU | IC.IALLUIS) * (M | DC.CVAU | IC)
 
let va-loc = ([Imp & TTD & R]; tr-ib; same-low-order-bits; tr-ib^-1; [Imp & TTD & R]) & loc
let po-va-loc = po & va-loc
