(*
 * SPDX-FileCopyrightText: Copyright (C) 2016-present, Arm Limited and/or its affiliates <open-source-office@arm.com>
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * The Armv8 Application Level Memory Model.
 *
 * This is a machine-readable, executable and formal artefact, which aims to be
 * the latest stable version of the Armv8 memory model.
 * If you have comments on the content of this file, please send an email to
 * memory-model@arm.com
 * For a textual version of the model, see section B2.3 of the Armv8 ARM:
 * https://developer.arm.com/documentation/ddi0487/
 *
 * Authors:
 * Nikos Nikoleris <nikos.nikoleris@arm.com>
 *)

(*
 * Include aarch64memattrs.cat to define relations on Memory Attributes.
 *)
include "aarch64memattrs.cat"

(* Can we move this to stdlib? *)
(* Coherence-after *)
let ca = fr | co

let PTE-MT-update =
  [Normal]; ca; [Device]
  | [Device]; ca; [Normal]

let PTE-SH-update =
  [NSH]; ca; [ISH | OSH]
  | [ISH]; ca; [OSH | NSH]
  | [OSH]; ca; [NSH | ISH]

let PTE-ICH-update =
  [iNC]; ca; [iWT | iWB]
  | [iWT]; ca; [iWB | iNC]
  | [iWB]; ca; [iNC | iWT]

let PTE-OCH-update =
  [oNC]; ca; [oWT | oWB]
  | [oWT]; ca; [oWB | oNC]
  | [oWB]; ca; [oNC | oWT]

let PTE-DT-update =
  [Device-GRE]; ca; [Device-nGRE | Device-nGnRE | Device-nGnRnE]
  | [Device-nGRE]; ca; [Device-GRE | Device-nGnRE | Device-nGnRnE]
  | [Device-nGnRE]; ca; [Device-GRE | Device-nGRE | Device-nGnRnE]
  | [Device-nGnRnE]; ca; [Device-GRE | Device-nGRE | Device-nGnRE]

let PTE-OA-update = ([PTE]; ca; [PTE & oa-changes(PTE, ca^-1)])
let PTE-OA-update-writable = PTE-OA-update &
        ([PTE]; ca; [PTE & at-least-one-writable(PTE, ca^-1)])

let PTE-update-needsBBM = ([PTEV]; ca \ (ca; [PTEV]; ca); [PTEV]) &
  (PTE-MT-update | PTE-SH-update | PTE-ICH-update | PTE-OCH-update | PTE-DT-update
  | PTE-OA-update)

let TTDV = PTEV
let TTDINV = PTEINV
let TTD-update-needsBBM = PTE-update-needsBBM
