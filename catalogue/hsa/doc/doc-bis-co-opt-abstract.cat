"patched first reading of the HSA documentation, abstract coding, SC partial orders version, restricted scopes for SC, "

(* This is the model described in the document, restricted scope instance
   version, pratial orders for SC *)

include "hsa-lib.cat"

(*************)
(* Coherence *)
(*************)

let makeCoL(s) = linearisations(s,co0)
with co from cross (map makeCoL (partition W))

let fr = rf^-1 ; co
let coh = (fr|rf|co)+


// useless (included in HhbCohCons)
// call consistent(coh,po) as CohCons

(*

let mincohWR = cohWR \ (cohWW; cohWR)

(* Relation a includes relation b, ie b(x,y) => a(x,y) *)
procedure includes(a,b) = empty b \ a end
procedure equals(a,b) =
  call includes(a,b)
  call includes(b,a)
end

call equals(rf,mincohWR) as LoadCons
*)

empty rmw & (fr;co) as RmwCons

(*****************************)
(* No values out of thin air *)
(*****************************)

(* gdo aka no value out of thin-air *)
let ldo = addr | data | ctrl
let gdo = rfe | ldo
//let gdo = coh | ldo

acyclic gdo as GdoCons

(*******************************)
(* Heterogenous happens before *)
(*******************************)

let acq-rel =
  ((W & Release) * (R & Acquire)) & coh
| ((F & Release) * Acquire) &
  ((po & (_ * W)); coh; (po? & (R * _)))
| (Release * (F & Acquire)) &
  ((po? & (_ * W)); coh; (po & (R * _)))

let sso s = same-instance(s) & acq-rel
  
let hhb = (po | union-scopes sso)+

irreflexive hhb as HhbCons
call consistent (hhb,coh) as HhbCohCons

(* SC orders operate on synchronizing operations in scope instances *)
let sync-instances(lvl) =
  (Synchronizing * Synchronizing) & same-instance(lvl)

(* For any scope instance S, X -po-> Y = X -SC_S-> Y *)
let preSC = (hhb | coh)

(* All combinations of all orders for a given scope level *)
let makeSCscope(lvl) =  preSC & sync-instances(lvl)

(* For diagrams *)
let SWI = makeSCscope('wi) show SWI
let SWAVE =  makeSCscope('wave) show SWAVE
let SWG = makeSCscope('wg) show SWG
let SAGENT = makeSCscope('agent) show SAGENT
let SSYSTEM = makeSCscope('system) show SSYSTEM

(* And memoization, for efficiency *)
let getSC (lvl) = match lvl with
|| 'wi -> SWI
|| 'wave -> SWAVE
|| 'wg -> SWG
|| 'agent -> SAGENT
|| 'system -> SSYSTEM
end


(*****************************************)
(* Check acyclicity for all scope levels *)
(*****************************************)

forall lvl in scopes do
  acyclic getSC(lvl) as ScCons
end

(***********************************)
(* Check consistency of SC orders  *)
(***********************************)

(* First build a set of pairs of orders *)

let rec pairs(s) = match s with
|| {} -> {}
|| x ++ xs ->
   map (fun y -> (x,y)) xs | pairs xs
end

let allPairs = pairs (map getSC scopes)
    
procedure compatible(sc1,sc2) =
  acyclic sc1|sc2
end

forall p in allPairs do
  call compatible(p) as ScSc
end

include "hsa-race.cat"
