(*
 * The Armv8 Application Level Memory Model.
 *
 * This is a machine-readable, executable and formal artefact, which aims to be
 * the latest stable definition of the notions of dependencies in the Armv8 memory model.
 * If you have comments on the content of this file, please send an email to
 * jade.alglave@arm.com, referring to version number:
 * 
 * For a textual version of the model, see section B2.3 of the Armv8 ARM:
 *   https://developer.arm.com/docs/ddi0487/latest/arm-architecture-reference-manual-armv8-for-armv8-a-architecture-profile
 *
 * Author: Jade Alglave <jade.alglave@arm.com>
 *
 * Copyright (C) 2018-2020, Arm Ltd.
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are
 * met:
 *
 *     * Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *     * Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in
 *       the documentation and/or other materials provided with the
 *       distribution.
 *     * Neither the name of ARM nor the names of its contributors may be
 *       used to endorse or promote products derived from this software
 *       without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS
 * IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED
 * TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A
 * PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
 * HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 *)
include "always-executed-writes.cat"

(* Intrinsic order *)
let intrinsic = (iico_data|iico_ctrl)+

(* Dependency through register *)
let generated-by-stxr = udr(same-instr;[range(lxsx)])
let rf-reg-no-stxr =
  let NW = ~generated-by-stxr in
  [NW];rf-reg
let dd = (rf-reg-no-stxr | intrinsic)+

(** Data and Address dependencies *)
let data = [R]; dd; [DATA]; intrinsic; [W]
let addr = [R]; dd; [NDATA]; intrinsic; [M]

(** Control dependency *)

(* The relation to-PoD is a dependency through register 
   to a point of divergence*)
let to-PoD = [R]; dd; [PoD]

(* Pre-control dependency *)
let pre-ctrl = to-PoD; po

(* Equivalent writes *)
let antecedent = 
  let immediate-lws = singlestep([W];po-loc;[W]) in
  (intrinsic|rf-reg|lrs|immediate-lws)^-1

let pre-equiv-evts =
  let rdw-ext = 
    let wwloc = ((W * W) & loc) \ id in
    ((rfe^-1;wwloc;rf)|(rf^-1;wwloc;rfe)) 
  in same-static\rdw-ext
show pre-equiv-evts \ id as pre-equiv-evts

let equivalent-writes = (W*W) & bisimulation(antecedent,pre-equiv-evts)
show equivalent-writes \ id as equivalent-writes

(* Control dependency *)
let ctrl = pre-ctrl \ (always-executed(to-PoD,equivalent-writes))

(* After dependency relation ctrl is defined *)
include "show-deps.cat"
