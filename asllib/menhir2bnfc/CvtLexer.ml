(*
 * SPDX-FileCopyrightText: Copyright 2022-2023 Arm Limited and/or its affiliates <open-source-office@arm.com>
 * SPDX-License-Identifier: BSD-3-Clause
 *)
(*
    **DISCLAIMER**: The ASL reference implementation can be found in `herdtools7/asllib`. This tool and any files generated by it are **NOT** a reference ASL implementation!
    Modifications of generated files will lead to an **UNSUPPORTED** divergence from the reference implementation and should **NOT** be done!
*)

(*
   Translate an ocamllex .ml file to a set of bnfc tokens and comment descriptors
 *)

open BNFC

(* A utility function to convert a token name into a valid bnfc identifier *)
let slug_str =
  String.map (function
    | ('A' .. 'Z' | 'a' .. 'z' | '0' .. '9' | '_') as c -> c
    | _ -> '_')

(* Extract token and comemnt data from an ocamllex -ml file. *)
module Convert (GRAMMAR : MenhirSdk.Cmly_api.GRAMMAR) : sig
  val comments : comment list
  val tokens : token list
  val reserved : decl list
  val reserved_entry_point : (string * decl list)
end = struct

  let reserved_id_name = "RESERVED_IDENTIFIER"
  let reserved_keyword_name ="ReservedKeyword"
  let reserved_ep_name = "Reserved"

  let comments = [ Comment [ "//" ]; Comment [ "/*"; "*/" ] ]

  let tokens =
    let open GRAMMAR in
    let open Regex in
    let digit : Regex.t = MatchDigit in
    let alpha : Regex.t = MatchLetter in
    let int_lit : Regex.t =
      Seq [ digit; ZeroOrMore (Choice [ digit ; Char '_' ])]
    in
    let hex_alpha : Regex.t = OneOf "abcdefABCDEF" in
    let hex_lit : Regex.t =
      Seq [
        Str "0x";
        Choice [ digit; hex_alpha ];
        ZeroOrMore (Choice [ Char '_'; digit; hex_alpha ]);
      ]
    in
    let bit : Regex.t = OneOf "01 " in
    let mk_token terminal acc =
      let t_name = Terminal.name terminal in
      let name = slug_str t_name in
      let re regex = Token { name; regex } in
      let is_regular =
        match Terminal.kind terminal with `REGULAR -> true | _ -> false
      in
      let is_external_term term =
        let attrs = Terminal.attributes term in
        List.for_all (fun a -> not @@ Attribute.has_label "internal" a) attrs
      in
      let is_external = is_external_term terminal in
      if (not is_external) || (not is_regular) || String.equal t_name "EOF" then acc
      else
        let tok =
          match t_name with
          | "STRING_LIT" ->
              re (
                Seq [
                  Char '"';
                  ZeroOrMore (
                      Choice
                        [
                          Except (MatchAll, OneOf "\"\\");
                          Seq [Char '\\'; OneOf "nt\"\\" ];
                        ]);
                  Char '"';
                ])
          | "INT_LIT" -> re @@ Choice [ int_lit; hex_lit ]
          | "REAL_LIT" -> re (Seq [int_lit; Char '.'; int_lit])
          | "BITVECTOR_LIT" ->
              re (
                Seq [
                  Char '\'';
                  ZeroOrMore bit;
                  Char '\'';
                ])
          | "MASK_LIT" ->
              re (
                Seq [
                  Char '\'';
                  ZeroOrMore (Choice [
                    bit;
                    Char 'x';
                    Seq [Char '('; OneOrMore bit; Char ')']
                  ]);
                  Char '\'';
                ]
              )
          | "IDENTIFIER" ->
              re (
                Seq [
                  Choice [ alpha; Char '_' ];
                  ZeroOrMore (Choice [ alpha; digit; Char '_' ])
                ])
          | "BOOL_LIT" -> re @@ Choice [ Str "TRUE"; Str "FALSE" ]
          | _ ->
              let asl_tok =
                match Asllib.Lexer.token_of_string t_name with
                | None ->
                    raise
                    @@ Failure
                         (Printf.sprintf "No token with name %s known." t_name)
                | Some t -> t
              in
              let sym = Asllib.Lexer.token_to_symbol asl_tok in
              Token { name; regex = Str sym }
        in
        tok :: acc
    in
    let reserved_id =
      let name = reserved_id_name in
      let regex : Regex.t =
        Seq [
          Str "__";
          ZeroOrMore (
            Choice [ alpha; digit; Char '_' ]);
        ]
      in
      Token { name; regex }
    in
    reserved_id :: GRAMMAR.Terminal.fold mk_token []

  let reserved =
    let mk_decl kw =
      let name = reserved_keyword_name in
      let ast_name = name ^ "_" ^ slug_str kw in
      let terms = [ Literal kw ] in
      Decl { ast_name; name; terms }
    in
    List.map mk_decl Asllib.Lexer.reserved_keywords

  let reserved_entry_point =
    let name = reserved_ep_name in
    (
      name,
      [
          Decl { ast_name = name ^ "_Token" ; name; terms = [Reference reserved_id_name ] };
          Decl { ast_name = name ^ "_Keywords" ; name; terms = [Reference reserved_keyword_name ] }
      ]
    )
end
